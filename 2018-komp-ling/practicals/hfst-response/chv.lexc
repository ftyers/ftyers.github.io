Multichar_Symbols

%<n%>                ! Имя существительное
%<pl%>               ! Множественное число
%<nom%>              ! Именительный падеж
%<ins%>              ! Творительный падеж
%<gen%>              ! Родительный падеж

%<num%>              ! Число

%{A%}                ! Архифонема [а] или [е]
%{Ă%}                ! Архифонема [ӑ] или [ӗ] или 0
%{м%}                ! Архифонема [м] или 0
%{с%}                ! Несонорные согласные
%{л%}                ! -н, -л, или -р
%{э%}                ! Передние гласные
%{а%}                ! Задние гласные

%{ъ%}                ! Для заимствованных слов

%<der_лӑх%>          ! Суффикс -лӐх

%>                   ! Граница морфемы

LEXICON Root

Nouns ; 
Guesser ;

LEXICON CASES 

%<nom%>:%> # ;
%<ins%>:%>п%{A%} # ;
%<gen%>:%>%{Ă%}н # ;

LEXICON PLURAL

             CASES ; 
%<pl%>:%>се%{м%} CASES ;

LEXICON SUBST 

PLURAL ;

LEXICON DER-N

%<der_лӑх%>:%>л%{Ă%}х SUBST "weight: 1.0" ;

LEXICON NUM-DIGIT

%<num%>:%- CASES ;

LEXICON LAST-DIGIT

1:1%{э%}%{л%}    NUM-DIGIT ; ! "пӗр" 
2:2%{с%}%{э%}    NUM-DIGIT ; ! "иккӗ" 
3:3%{с%}%{э%}    NUM-DIGIT ; ! "виҫҫӗ" 
4:4%{с%}%{а%}    NUM-DIGIT ; ! "тӑваттӑ" 
5:5%{э%}%{с%}    NUM-DIGIT ; ! "пиллӗк" 
6:6%{с%}%{а%}    NUM-DIGIT ; ! "улттӑ" 
7:7%{с%}%{э%}    NUM-DIGIT ; ! "ҫиччӗ" 
8:8%{э%}%{л%}    NUM-DIGIT ; ! "саккӑр" 
9:9%{э%}%{л%}    NUM-DIGIT ; ! "тӑххӑр" 

LEXICON LOOP

                 LAST-DIGIT ; 
                 DIGITLEX ; 

LEXICON DIGITLEX

%0:%0 LOOP ;
1:1   LOOP ;
2:2   LOOP ;
3:3   LOOP ;
4:4   LOOP ;
5:5   LOOP ;
6:6   LOOP ;
7:7   LOOP ;
8:8   LOOP ;
9:9   LOOP ;

LEXICON Guesser

🂡%<guess%>:🂡 N ;

LEXICON N

%<n%>: PLURAL ;
%<n%>: SUBST ;
%<n%>: DER-N ;

LEXICON N/сть

%<n%>:ҫ SUBST "weight: 0.5" ;
%<n%>%<nom%>:сть # "weight: 1.0" ;

LEXICON Nouns

урам:урам N ;     ! "улица"
пакча:пакча N ;   ! "сад"
хула:хула N ;     ! "город"
канаш:канаш N ;   ! "совет"
тӗс:тӗс N ;       ! "вид"
патша:патша N ;   ! "царь"
куҫ:куҫ N ;       ! "глаз"
патшалӑх:патшалӑх N ; ! "государство"
специалист:специалист%{ъ%} N ; ! "специалист"